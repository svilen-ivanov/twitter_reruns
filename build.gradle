buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'net.saliman:gradle-cobertura-plugin:2.2.8'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.11.3'
    }
}

plugins {
    id "us.kirchmeier.capsule" version "1.0.1"
}

apply plugin: 'java'
apply plugin: 'cobertura'
apply plugin: 'application'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'com.github.ben-manes.versions'

// group is the groupId
group = 'com.buhtum.twr'
//the default artifactId is the project name, which is per default the directory name of the project
version = '0.0.4-SNAPSHOT'

description = """A bootstrap Java project with gradle, TestNG, Mockito, FEST-Assert 2 and Cobertura for Eclipse and Jenkins"""

mainClassName = "com.buhtum.twr.Main"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
}

dependencies {
    compile(
            'com.google.guava:guava:19.0',
            'ch.qos.logback:logback-classic:1.1.3',
            'org.twitter4j:twitter4j-core:4.0.+',
            'org.twitter4j:twitter4j-stream:4.0.+',
            'joda-time:joda-time:2.9.+',
            'net.sf.supercsv:super-csv-java8:2.4.0',
            'net.sf.supercsv:super-csv-joda:2.4.0',
            'com.squareup.okhttp3:okhttp:3.2.0',
            'com.google.code.gson:gson:2.6.2',
    )

    testCompile(
            'org.testng:testng:6.9.10',
            'org.uncommons:reportng:1.1.4',
            'org.mockito:mockito-all:2.0.2-beta',
            'org.easytesting:fest-assert-core:2.0M10',
            // workaround for a known issue with TestNG 6.x: explicitly add Guice (Gradle will fail to run tests otherwise
            'com.google.inject:guice:4.0'
    )
}

// ensure that test resources (src/test/resources) are added to CLASSPATH;
// see http://forums.gradle.org/gradle/topics/tests_arent_executed_when_setting_the_test_runtimeclasspath and
// http://gradle.org/docs/current/dsl/org.gradle.api.tasks.testing.Test.html#org.gradle.api.tasks.testing.Test:classpath
sourceSets {
    main {
        runtimeClasspath = files(output.resourcesDir) + runtimeClasspath
    }
    test {
        runtimeClasspath = files(output.resourcesDir) + runtimeClasspath
    }
}

test {
    useTestNG() {
        suiteXmlBuilder().suite(name: 'gradle-bootstrap', parallel: 'tests') {
            test(name: 'all-tests') {
                packages {
                    'package'(name: 'com.miguno.*')
                }
            }
        }
    }
    options {
        listeners << 'org.uncommons.reportng.HTMLReporter'
        listeners << 'org.uncommons.reportng.JUnitXMLReporter'
    }
    ext.useDefaultListeners = true
    ext.workingDirectory = 'build/'

    testLogging.showStandardStreams = true

    // http://stackoverflow.com/a/20359498
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

// https://github.com/stevesaliman/gradle-cobertura-plugin
// http://ingorichter.blogspot.ch/2013/03/using-cobertura-with-gradle.html for list of options
cobertura {
    coverageFormats = ['xml', 'html']
}

// See http://www.gradle.org/docs/current/userguide/gradle_wrapper.html
task wrapper(type: Wrapper) {
    description = 'Install Gradle wrapper'
    gradleVersion = '2.10'
}

// Runnable Jars
// https://github.com/danthegoodman/gradle-capsule-plugin/blob/master/DOCUMENTATION.md
task fatCapsule(type: FatCapsule) {
    applicationClass mainClassName
    reallyExecutable

    capsuleManifest {
//        systemProperties['log4j.configuration'] = 'log4j.xml'
//        args = ['-Xmx1024M']
        minJavaVersion = targetCompatibility
    }
}

task mavenCapsule(type: MavenCapsule) {
    applicationClass mainClassName
    reallyExecutable

    capsuleManifest {
        repositories << "jcenter"
    }
}


